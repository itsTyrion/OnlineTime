import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "org.jetbrains.kotlin.jvm" version "2.1.10"
    id "org.jetbrains.kotlin.kapt" version "2.1.10"
    id "com.gradleup.shadow" version "8.3.6"
}

repositories {
    mavenCentral()
    maven {
        name = "Sonatype"
        url = uri("https://oss.sonatype.org/content/repositories/snapshots")
    }
    maven {
        name = "PaperMC"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven { url = uri("https://jitpack.io") }
}

dependencies {
    implementation "org.xerial:sqlite-jdbc:3.49.1.0"
    implementation("com.mysql:mysql-connector-j:9.2.0") { exclude group: "com.google.protobuf", module: "protobuf-java" }
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.10"
    implementation "org.yaml:snakeyaml:2.3"
    implementation "org.slf4j:slf4j-api:2.0.12"
    implementation "org.slf4j:slf4j-simple:2.0.12"
    compileOnly "net.md-5:bungeecord-api:1.21-R0.1-SNAPSHOT"
    compileOnly "com.velocitypowered:velocity-api:3.3.0-SNAPSHOT"
    compileOnly "de.itsTyrion:PluginAnnotationProcessor:1.4"
    kapt "de.itsTyrion:PluginAnnotationProcessor:1.4"
}

group = "lu.r3flexi0n"
version = "8.6.0"
description = "OnlineTime"

kotlin {
    jvmToolchain 17
}

kapt.arguments {
    arg "mcPluginVersion", "$version"
}

shadowJar {
    archiveClassifier = ""
    minimize() {
        exclude(dependency("org.xerial:sqlite-jdbc"))
        exclude(dependency("com.mysql:mysql-connector-j"))
    }

    mergeServiceFiles()

    relocate "kotlin", "de.itsTyrion.shaded.kotlin"
    relocate "org.yaml", "de.itsTyrion.shaded.snakeyaml"

    def base = "org/sqlite/native"
    exclude "$base/*/ppc64/**", "$base/*/riscv64/**", "$base/*/arm*/**", "$base/*/x86/**" // uncommon architectures for MC servers
    exclude "$base/Linux-*/**", "$base/Mac/**", "$base/FreeBSD/**" // and uncommon OS' for MC servers
}

tasks.register("fat", ShadowJar) {
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
    archiveClassifier = "fat"
    minimize() {
        exclude(dependency("org.xerial:sqlite-jdbc"))
        exclude(dependency("com.mysql:mysql-connector-j"))
    }

    mergeServiceFiles()

    relocate "kotlin", "de.itsTyrion.shaded.kotlin"
    relocate "org.yaml", "de.itsTyrion.shaded.snakeyaml"
}

build {
    dependsOn([shadowJar, fat])
}